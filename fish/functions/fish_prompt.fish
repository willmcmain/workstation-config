function fish_prompt --description 'Write out the prompt'
    set -l last_pipestatus $pipestatus
    set -lx __fish_last_status $status # Export for __fish_print_pipestatus.
    set -l normal (set_color normal)
    set -q fish_color_status
    or set -g fish_color_status red

    # Color the prompt differently when we're root
    set -l color_cwd $fish_color_cwd
    set -l suffix '>'
    if functions -q fish_is_root_user; and fish_is_root_user
        if set -q fish_color_cwd_root
            set color_cwd $fish_color_cwd_root
        end
        set suffix '#'
    end

    # Write pipestatus
    # If the status was carried over (if no command is issued or if `set` leaves the status untouched), don't bold it.
    set -l bold_flag --bold
    set -q __fish_prompt_status_generation; or set -g __fish_prompt_status_generation $status_generation
    if test $__fish_prompt_status_generation = $status_generation
        set bold_flag
    end
    set __fish_prompt_status_generation $status_generation
    set -l status_color (set_color $fish_color_status)
    set -l statusb_color (set_color $bold_flag $fish_color_status)
    set -l prompt_status (__fish_print_pipestatus "[" "]" "|" "$status_color" "$statusb_color" $last_pipestatus)

    # Set the git prompt
    set -g __fish_git_prompt_showdirtystate true
    set -g __fish_git_prompt_showstashstate true
    set -g __fish_git_prompt_showcolorhints true
    set -l git_prompt (fish_git_prompt)
    if set -q git_prompt[1]
        set -l repo (basename (git rev-parse --show-toplevel))
        echo -s (set_color purple) $repo $normal $git_prompt $normal
    end

    echo -ns (prompt_login) " " (set_color $color_cwd) (prompt_pwd) $normal  " "$prompt_status $suffix " "
end
